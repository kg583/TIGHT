// MAXF
// Computes the maximum flow in the graph, with edge weights taken as capacities, given a source vertex Ans(1) and sink vertex Ans(2)
// Utilizes the Edmonds-Karp algorithm (a BFS implementation of Ford-Fulkerson)
// Returns the maximum flow in O(VE^2)

{abs(iPart(real(Ans→θGT
DelVar XE→dim(ʟθGT4
Repeat not(Ans
  {ʟθGT(1→θGT3
  V→dim(ʟθGT2
  ⁻1→ʟθGT2(ʟθGT(1
  While dim(ʟθGT3
    ʟθGT3(dim(ʟθGT3
    prgmθGTIS
    If dim(Ans:Then
      Ans→θGT1
      For(θ,1,dim(ʟθGT1
        ʟθGT1(θ→Y
        ʟE(Ans:prgmθGTO
        If Ans≠ʟθGT(1) and not(ʟθGT2(Ans)) and ʟθGT4(Y)<max(1,imag(ʟE(Y:Then
          augment({Ans},ʟθGT3→θGT3
          Y→ʟθGT2(Ans(1
        End
        If Z≠ʟθGT(1) and not(ʟθGT2(Z)) and ⁻ʟθGT4(Y)<max(1,imag(ʟE(Y:Then
          augment({Z},ʟθGT3→θGT3
          Y→ʟθGT2(Ans(1
        End
      End
    End
    dim(ʟθGT3)-1→dim(ʟθGT3
  End
  ʟθGT(1→θ:ʟθGT(2→Z
  ʟθGT2(Ans
  If Ans:Then
    ᴇ99→Y:ʟθGT2
    prgmθGTP:Ans→θGT1
    {θ→θGT
    For(θ,1,dim(ʟθGT1
      ʟθGT1(θ
      min(Y,imag(ʟE(Ans))-ʟθGT4(Ans→Y
    End
    For(θ,1,dim(ʟθGT1
      ʟθGT1(θ
      Y+ʟθGT4(Ans→ʟθGT4(Ans
    End
    X+Y→X:1
  End
End:X
DelVar ʟθGT1DelVar ʟθGT2DelVar ʟθGT3DelVar ʟθGT4